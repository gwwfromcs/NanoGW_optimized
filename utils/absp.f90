!===================================================================
!
! Calculate the absorption with el.-hole interaction, using 
! eq. (26), Rohlfing & Louie (PRB (62):4927, 2000), and the density of 
! exciton states (measured in Ryd^-1 )
! Each delta function is replaced by a Lorentzian peak (that can be
! replaced by a Gaussian peak easily),
! delta(x) -> (D/pi)/( x**2 + D**2 ), D = eta
!
! input is file "eigenvalues", in the same format as generated by
! "bsesolv" and "sigma" codes but with energy resolution eta added
! at the end of first line. Input energies (including eta) are 
! assumed to be eV (numerically, it doesn't really matter as long as
! eta and eigenvalues are in the same units).
!
! output units are:
!      2 columns in file "across":
!           energy    cross section
!      4 columns in file "absorp":
!           energy    alpha_2    alpha_1    jdos
!
!      units: energy             -> eV
!             cross section      -> (bohr^2)
!             alpha_2, alpha_1   -> (bohr^3)
!             jdos               -> 1/eV
!
! WARNING: DO NOT FORGET TO ADD THE VALUE OF ETA IN INPUT FILE!
!
! Copyright (C) 2009 Murilo Tiago, Univ. of Texas, Austin, TX, USA
! mtiago@ices.utexas.edu
!
! First version written by Murilo Tiago, Univ. of Minnesota, June 2004
!
!    This program is free software; you can redistribute it and/or modify
!    it under the terms of the GNU General Public License as published by
!    the Free Software Foundation; either version 1, or (at your option)
!    any later version.
!
!    This program is distributed in the hope that it will be useful,
!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!    GNU General Public License for more details.
!
!    You should have received a copy of the GNU General Public License
!    along with this program; if not, write to the Free Software
!    Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston MA  02110-1301 USA
!
!-----------------------------------------------------------------------
program absp

  implicit none

  integer, parameter :: dp = kind(1.0d0)
  real(dp), parameter :: ryd=13.60569173_dp

  integer :: neig, ii, iw, nwstep
  real(dp) :: eta, emin, emax, omega, epsr, epsi, dos, faci, facr, &
       en, rsum(3), epsri, coeff6
  real(dp), allocatable :: eigenv(:), ostrength(:), os(:,:)
  real(dp) :: eta1
  ! numerical factor : 4 * pi^2 * alpha , alpha = fine structure const.
  real(dp), parameter :: afac = 0.28808793d0

  !-----------------------------------------------------------------------
  ! Open input file and define energy bounds
  !              
  open(10,file='eigenvalues',form='formatted',status='old')
  read(10,*) neig, eta
  allocate(eigenv(neig))
  allocate(ostrength(neig))
  allocate(os(3,neig))
  do ii=1,neig
     read(10,*) eigenv(ii), os(1,ii), os(2,ii), os(3,ii)
     ostrength(ii) = (sum(os(:,ii)))/3.d0
  enddo
  close(10)
  emin= minval(eigenv)
  emax = maxval(eigenv)
  emin = 0.d0
  emax = emax + 10.0*eta
  write(6,*) ' Calculating absorption spectrum '
  write(6,*) ' energy resolution (eV) = ',eta
  nwstep = 1000

  open(10,file='absorp',form='formatted')
  open(11,file='across',form='formatted')

  !-----------------------------------------------------------------------
  ! Calculate sum rule and static polarizability
  !
  rsum = 0.d0
  do ii=1,neig
     rsum = rsum + os(:,ii)
  enddo
  write(6,'(a,3f10.3)') ' Sum rule = ',rsum
  write(6,'(a,f10.3)') ' average = ',sum(rsum)/3.d0
  write(6,*)
  write(6,*) ' sum rule normalized to number of valence electrons '
  write(6,*) ' exact value = total number of valence electrons'
  rsum = 0.d0
  do ii=1,neig
     rsum = rsum + os(:,ii)/(eigenv(ii)/ryd)/(eigenv(ii)/ryd)
  enddo
  rsum = rsum*4.d0
  write(6,*)
  write(6,'(a,3f10.3)') ' Static polarizability (a.u.) = ',rsum
  write(6,'(a,f10.3)') ' average = ',sum(rsum)/3.d0

  !-----------------------------------------------------------------------
  ! Calculate absorption spectrum (epsi, proportional to imaginary part of
  ! polarizability) and its Krammers-Kronig partner function (epsr, real
  ! part of polarizability)
  !
  eta1 = eta
  coeff6 = 0.d0
  do iw=1,nwstep
  !
  !  absorption contribution
  !
     omega = emin + (emax - emin)*real(iw,dp)/real(nwstep,dp)
     dos = 0.d0
     epsr = 0.d0
     epsi = 0.d0
     epsri = 0.d0
     do ii=1,neig
        en = omega - eigenv(ii)
        facr = -en/( en**2 + eta**2 )
        epsr = epsr + ostrength(ii) * facr * 0.5d0 / (eigenv(ii)/ryd)
  !
  !  Gaussian convolution:
        faci = exp( -en*en/(2.d0*eta**2) )/dsqrt(8.d0*datan(1.d0))/eta
  !
  !  Lorentzian convolution:
  !
  !      faci =  eta/( en**2 + eta**2 )/(4.d0*datan(1.d0))
        epsi = epsi + ostrength(ii) *faci * 0.5d0 / (eigenv(ii)/ryd)
        dos = dos + faci
        facr = 4.d0 * ryd * ryd/( omega**2 + eigenv(ii)**2 )
        epsri = epsri + ostrength(ii) * facr
     enddo
  !
  !  emission contribution
  !
     do ii=1,neig
        en = -omega - eigenv(ii)
        facr = -en/( en**2 + eta**2 )
        epsr = epsr + ostrength(ii) * facr * 0.5d0 / (eigenv(ii)/ryd)
  !
  !  Gaussian convolution:
        faci = -exp( -en*en/(2.d0*eta**2) )/dsqrt(8.d0*datan(1.d0))/eta
  !
  !  Lorentzian convolution:
  !
  !      faci = -eta/( en**2 + eta**2 )/(4.d0*datan(1.d0))
        epsi = epsi + ostrength(ii) * faci * 0.5d0 / (eigenv(ii)/ryd)
     enddo
     write(11,'(2f18.8)') omega,epsi*omega*afac
     epsr = epsr * 4.0 * ryd
     epsi = epsi * 4.0 * ryd * 4.d0*datan(1.d0)
     write(10,'(5f18.8)') omega,epsi,epsr,dos,epsri
     if (iw == 1 .or. iw == nwstep) then
        coeff6 = coeff6 + epsri * 0.5d0
     else
        coeff6 = coeff6 + epsri * epsri
     endif
  enddo
  coeff6 = coeff6 * (emax - emin)/real(nwstep-1,dp)/ryd/datan(1.d0)/4.d0
  write(6,*) ' dipole-dipole dispersion coefficient = ',coeff6,' (a.u.)^6'

  close(10)

end program absp
!===================================================================
